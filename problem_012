#!/usr/bin/perl
#
# The sequence of triangle numbers is generated by adding the natural numbers.
# So the 7^(th) triangle number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 = 28.
# The first ten terms would be:
# 
#     1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
# 
# Let us list the factors of the first seven triangle numbers:
#
#     1: 1
#     3: 1,3
#     6: 1,2,3,6
#    10: 1,2,5,10
#    15: 1,3,5,15
#    21: 1,3,7,21
#    28: 1,2,4,7,14,28
#
# We can see that 28 is the first triangle number to have over five divisors.
#  
# What is the value of the first triangle number to have over
# five hundred divisors?
#

use 5.010;

use strict;
use warnings;
no warnings 'syntax';

my $TARGET = shift // 500;

sub count {
    my $n = shift;
    my $count = 0;
    for (my $i = 1; $i < sqrt ($n); $i ++) {
        $count += 2 unless $n % $i;
    }
    $count ++ if sqrt ($n) == int sqrt ($n);
    $count;
}

my $n   = 1;
my $sum = 0;

{
    $sum += $n ++;
    if (count ($sum) > $TARGET) {
        say $sum;
        exit;
    }
    redo;
}
__END__
