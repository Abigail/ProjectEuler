#!/usr/bin/perl

use 5.010;

use strict;
use warnings;
no  warnings 'syntax';

use Math;
use List::Util qw [sum];

#
# A perfect number is a number for which the sum of its proper divisors
# is exactly equal to the number. For example, the sum of the proper
# divisors of 28 would be 1 + 2 + 4 + 7 + 14 = 28, which means that 28 is
# a perfect number.
# 
# A number n is called deficient if the sum of its proper divisors is less
# than n and it is called abundant if this sum exceeds n.
# 
# As 12 is the smallest abundant number, 1 + 2 + 3 + 4 + 6 = 16, the
# smallest number that can be written as the sum of two abundant numbers is
# 24. By mathematical analysis, it can be shown that all integers greater
# than 28123 can be written as the sum of two abundant numbers. However,
# this upper limit cannot be reduced any further by analysis even though
# it is known that the greatest number that cannot be expressed as the
# sum of two abundant numbers is less than this limit.
# 
# Find the sum of all the positive integers which cannot be written as
# the sum of two abundant numbers.
# 

my $MAX = 20161;  # Limit from mathworld.

my @abundant = grep {not_perfect ($_) > 0} 2 .. $MAX;

my @numbers = (0 .. $MAX);

OUTER:
  for (my $i = 0; $i < @abundant; $i ++) {
      for (my $j = $i; $j < @abundant; $j ++) {
          my $sum = $abundant [$i] + $abundant [$j];
          next OUTER if $sum > $MAX;
          $numbers [$sum] = 0;
      }
  }

say sum @numbers;

__END__
# Answer: 4179871
