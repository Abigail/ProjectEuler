#!/usr/bin/perl
#
# It can be seen that the number, 125874, and its double, 251748, contain
# exactly the same digits, but in a different order.
#
# Find the smallest positive integer, x, such that 2x, 3x, 4x, 5x, and 6x,
# contain the same digits.
#

use 5.010;

use strict;
use warnings;
no warnings 'syntax';

sub canon {join "" => sort split // => shift}

for (my $p = 1;; $p ++) {
    my $min = 1 . (0 x $p);
    my $max = 1 . (6 x $p);
  NUMBER:
    for my $n ($min .. $max) {
        my $c = canon $n;
        for my $mult (2 .. 6) {
            my $prod = $mult * $n;
            my $c1 = canon $prod;
            next NUMBER unless $c eq $c1;
        }
        say $n;
        exit;
    }
}


__END__
