#!/usr/bin/perl

use 5.010;

use strict;
use warnings;
no  warnings 'syntax';

#
# Using names.txt (right click and 'Save Link/Target As...'), a 46K text
# file containing over five-thousand first names, begin by sorting it into
# alphabetical order. Then working out the alphabetical value for each
# name, multiply this value by its alphabetical position in the list to
# obtain a name score.
# 
# For example, when the list is sorted into alphabetical order, COLIN,
# which is worth 3 + 15 + 12 + 9 + 14 = 53, is the 938th name in the
# list. So, COLIN would obtain a score of 938 ? 53 = 49714.
# 
# What is the total of all the name scores in the file?
# 

my $data  = "Data/names.txt";
my $text  = do {local $/; local @ARGV = $data; <>};
my @names = sort $text =~ /([A-Z]+)/g;

my $total = 0;
for (my $i = 0; $i < @names; $i ++) {
    my $name = $names [$i];
    my $sum  = 0;
       $sum += ord ($_) - ord ('A') + 1 for $name =~ /(.)/g;
    $total  += ($i + 1) * $sum;
}
say $total;

__END__
Answer: 871198282
