#!/usr/bin/perl

use 5.010;

use strict;
use warnings;
no warnings 'syntax';

# 
# Euler published the remarkable quadratic formula:
# 
# n² + n + 41
# 
# It turns out that the formula will produce 40 primes for the consecutive
# values n = 0 to 39. However, when n = 40, 40^(2) + 40 + 41 = 40(40 + 1)
# + 41 is divisible by 41, and certainly when n = 41, 41² + 41 + 41 is
# clearly divisible by 41.
# 
# Using computers, the incredible formula  n² - 79n + 1601 was
# discovered, which produces 80 primes for the consecutive values n =
# 0 to 79. The product of the coefficients, -79 and 1601, is -126479.
# 
# Considering quadratics of the form:
# 
#    n² + an + b, where |a| < 1000 and |b| < 1000
#
#    where |n| is the modulus/absolute value of n e.g. |11| = 11 and |-4| = 4
# 
# Find the product of the coefficients, a and b, for the quadratic
# expression that produces the maximum number of primes for consecutive
# values of n, starting with n = 0.
# 

# 
# Clearly, b has to be prime, as the formula will yield b if n = 0.
#
# Except for the observation above, use brute force.
#

use Math;

my $max     = 0;
my $product = 1;
$| = 1;
foreach my $aa (-999 .. 999) {
  LOOP_bb:
    foreach my $bb (primes 1000) {
        my $gen = sub {$_ [0] ** 2 + $aa * $_ [0] + $bb};
        my $n = 0;
        {
            my $val = $gen -> ($n);
            next LOOP_bb unless $val > 0 && is_prime $val;
            if ($n > $max) {
                $product = $aa * $bb;
                $max     = $n;
            }
            $n ++;
            redo;
        }
    }
}
say $product;


__END__
# Answer: -59231
