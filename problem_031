#!/usr/bin/perl
#
# In England the currency is made up of pound, £, and pence, p, and there
# are eight coins in general circulation:
#
#    1p, 2p, 5p, 10p, 20p, 50p, £1 (100p) and £2 (200p).
#
# It is possible to make £2 in the following way:
#
#    1×£1 + 1×50p + 2×20p + 1×5p + 1×2p + 3×1p
#
# How many different ways can £2 be made using any number of coins?

use 5.010;

use strict;
use warnings;
no warnings 'syntax';

sub change;
sub change {
    my ($amount, $min) = @_;
    my $r = 0;
    for my $coin (1, 2, 5, 10, 20, 50, 100, 200) {
        next if $coin < $min;
        next if $coin > $amount;
        $r += 1, next if $coin == $amount;
        no warnings 'recursion';
        $r += change $amount - $coin, $coin;
    }
    return $r;
}


say change 200, 1;



__END__
