#!/usr/bin/perl
#
# Working from left-to-right if no digit is exceeded by the digit to its left
# it is called an increasing number; for example, 134468.
#
# Similarly if no digit is exceeded by the digit to its right it is called
# a decreasing number; for example, 66420.
#
# We shall call a positive integer that is neither increasing nor decreasing
# a "bouncy" number; for example, 155349.
#
# Clearly there cannot be any bouncy numbers below one-hundred, but just
# over half of the numbers below one-thousand (525) are bouncy. In fact, the
# least number for which the proportion of bouncy numbers first reaches 50%
# is 538.
#
# Surprisingly, bouncy numbers become more and more common and by the time
# we reach 21780 the proportion of bouncy numbers is equal to 90%.
#
# Find the least number for which the proportion of bouncy numbers is exactly
# 99%.
#

use 5.010;

use strict;
use warnings;
no warnings 'syntax';

my $inc = qr /^1*2*3*4*5*6*7*8*9*$/;
my $dec = qr /^9*8*7*6*5*4*3*2*1*0*$/;

my $CUTOFF = shift // 99;

for (my $n = 1; ; $n ++) {
    state $bouncy = 0;
    $bouncy ++ unless $n =~ /$inc/ || $n =~ /$dec/;
    if ($n * $CUTOFF == 100 * $bouncy) {
        say $n;
        exit;
    }
}


__END__
