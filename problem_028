#!/usr/bin/perl

use 5.010;

use strict;
use warnings;
no  warnings 'syntax';

use List::Util qw [sum];

# 
# Starting with the number 1 and moving to the right in a clockwise
# direction a 5 by 5 spiral is formed as follows:
# 
#     21 22 23 24 25
#     20  7  8  9 10
#     19  6  1  2 11
#     18  5  4  3 12
#     17 16 15 14 13
# 
# It can be verified that the sum of the numbers on the diagonals is 101.
# 
# What is the sum of the numbers on the diagonals in a 1001 by 1001 spiral
# formed in the same way?
# 

#
# Observations: - There will be 1002001 numbers in a 1001 x 1001 square.
#               - After the first number, 1, numbers on diagonals come
#                 in groups of four, with the same difference to the previous
#                 number. In the next group of four, the difference is two 
#                 more. So the first diagonal numbers are:
#                    1   (3,  5,  7,  9) (diff 2)
#                       (13, 17, 21, 25) (diff 4)
#                       (31, 37, 43, 49) (diff 6), etc.
#               - We only need the numbers on the diagonals; the order is 
#                 irrelevant.
#

my $MAX  = 1002001;

my @diag = (1);
my $last =  1;
my $skip =  2;
OUTER:
  while (1) {
    foreach my $c (1 .. 4) {
        my $next = $last + $skip;
        last OUTER if $next > $MAX;
        push @diag => $next;
        $last = $next;
    }
    $skip += 2;
  }


say sum @diag;

__END__
# Answer: 669171001
