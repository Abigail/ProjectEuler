#!/usr/bin/perl
#
# Triangle, pentagonal, and hexagonal numbers are generated by the
# following formulae:
#  Triangle                T_(n)=n(n+1)/2          1, 3, 6, 10, 15, ...
#  Pentagonal              P_(n)=n(3n−1)/2         1, 5, 12, 22, 35, ...
#  Hexagonal               H_(n)=n(2n−1)           1, 6, 15, 28, 45, ...
#
#
# It can be verified that T_(285) = P_(165) = H_(143) = 40755.
#
# Find the next triangle number that is also pentagonal and hexagonal.
#

use 5.010;

use strict;
use warnings;
no warnings 'syntax';

sub Tri {my $n = shift; $n * (    $n + 1) / 2}
sub Pen {my $n = shift; $n * (3 * $n - 1) / 2}
sub Hex {my $n = shift; $n * (2 * $n - 1)}

for (my ($t, $p, $h) = (286, 166, 144);;) {
    my $tri = Tri $t;
    my $pen = Pen $p;
    my $hex = Hex $h;
    if ($tri == $pen && $pen == $hex) {say $tri; exit}
    if ($tri <= $pen && $tri <= $hex) {$t ++; next}
    if ($pen <= $tri && $pen <= $hex) {$p ++; next}
    if ($hex <= $tri && $hex <= $pen) {$h ++; next}
}

__END__
